10 REM TELEPRINTER EDITION
20 REM APRIL 23, 2019
30 SX = .054054
40 SY = SX*1.66
50 INPUT "MAG:",M
60 INPUT "CX:",CX
70 INPUT "CY:",CY
80 INPUT "ITER:",ITER
90 FOR Y=24 TO -24 STEP -1
100 D=Y*SY/M+CY
110 FOR X=-37 TO 37
120 C=X*SX/M+CX
130 A=C
140 B=D
150 REM 48 FOR ASCII63
160 FOR I=0 TO ITER
170 IF (A*A+B*B)>4 THEN GOTO 250
180 T=A*A-B*B+C
190 B=2*A*B+D
200 A=T
210 NEXT I
220 PRINT " ";
230 GOTO 270
240 REM +0 FOR ASCII63, +7 HEX
250 IF I> 9 THEN I=I+0
260 PRINT CHR$(48+I);
270 NEXT X
280 PRINT
290 NEXT Y
300 PRINT : PRINT "MANDELBROT SET FROM ";
310 PRINT USING "+#.#### ";CX-37*SX/M;24*SY/M+CY;
320 PRINT "I TO ";
330 PRINT USING "+#.#### ";37*SX/M+CX;CY-24*SY/M;
340 PRINT "I"
350 PRINT "MAGNIFICATION "M" CENTERED AT ";
360 PRINT USING "+#.#### ";CX;CY;
370 PRINT "I, ITERATIONS: "ITER
