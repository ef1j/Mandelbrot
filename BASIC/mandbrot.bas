10 REM TELEPRINTER EDITION
20 REM MAY 11, 2019
30 SX = .054054
40 SY = SX*1.66
50 INPUT "MAG:",M
60 INPUT "CX:",CX
70 INPUT "CY:",CY
80 INPUT "ITER:",ITER
90 FOR Y=24 TO -24 STEP -1
100 D=Y*SY/M+CY
110 FOR X=-37 TO 37
120 C=X*SX/M+CX
130 A=C
140 B=D
150 REM 48 FOR ASCII63
160 FOR I=0 TO ITER
170 IF (A*A+B*B)>4 THEN GOTO 240
180 T=A*A-B*B+C
190 B=2*A*B+D
200 A=T
210 NEXT I
220 PRINT " ";
230 GOTO 270
240 COLOR = (I MOD 62) + 48
250 IF COLOR > 96 THEN COLOR = COLOR - 63
260 PRINT CHR$(COLOR);
270 NEXT X
280 PRINT
290 NEXT Y
300 PRINT : PRINT
305 PRINT "MANDELBROT SET FROM ";
310 PRINT USING "+#.#### ";CX-37*SX/M;24*SY/M+CY;
320 PRINT "I TO ";
330 PRINT USING "+#.#### ";37*SX/M+CX;CY-24*SY/M;
340 PRINT "I"
350 PRINT USING "###";M;
360 PRINT "X MAGNIFICATION CENTERED AT ";
370 PRINT USING "+#.#### ";CX;CY;
380 PRINT "I, "ITER" ITERATIONS"
